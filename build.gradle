plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    java {
        sourceCompatibility = "11"
        targetCompatibility = "11"
    }

    dependencies {
        implementation 'com.github.knokko:sample-profiler:v1.0.0'
        testImplementation platform('org.junit:junit-bom:5.10.1')
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    test {
        useJUnitPlatform()
    }
}

project(':generator-tester') {
    apply plugin: 'com.github.johnrengelman.shadow'
    dependencies {
        implementation project(':generator')
    }
    shadowJar {
        manifest {
            attributes ('Main-Class': 'fixie.generator.tester.GeneratorTesterKt')
        }
    }
}

project(':geometry') {
    dependencies {
        implementation project(':physics-kit')
    }
}

project(':physics') {
    dependencies {
        implementation project(':physics-kit')
        implementation project(':geometry')
        implementation 'com.github.knokko.update-loop:implementation:v2.0.0'
    }
}
